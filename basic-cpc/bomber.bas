10 :'BOMBER by DAVE TOWN
20 :'copyright (c) AMSOFT 1984
30 :'
40 MODE 1:CLS:INK 0,0:BORDER 0:INK 1,18:INK 2,6:INK 3,4:INK 5,15:INK 6,2:INK 7,24:INK 8,8:INK 9,26:INK 10,10:INK 11,20:INK 12,12:INK 13,16:INK 14,14:INK 15,21
50 SYMBOL AFTER 240:SYMBOL 241,&40,&60,&70,&7F,&7F,&3F,&7,&0:SYMBOL 242,&0,&32,&7A,&FE,&FA,&F2,&E0,&0
60 SCORE =0:HISCORE=0:PLANE$=CHR$(241)+CHR$(242):X=2:Y=2:DROP=0:A=2:B=2
70 GOSUB 480
80 CLS
90 PEN 2:LOCATE 1,15:INPUT"ENTER SKILL : 0 (ACE) TO 5 (NOVICE) : ",SKILL    
100 IF SKILL <0 OR SKILL>5 GOTO 90
110 SKILL=SKILL+10
120 LOCATE 1,15:PRINT CHR$(18);:LOCATE 1,15:INPUT"ENTER SPEED 0 (FAST) TO 100 (SLOW) : ",RATE
130 IF RATE>100 OR RATE<0 GOTO 120
140 :'
150 :'BUILDINGS
160 :'
170 MODE 0:FOR BASE=5 TO 15:FOR HEIGHT=21 TO INT(RND(1)*8+SKILL) STEP -1:LOCATE BASE,HEIGHT:PEN BASE-2:PRINT CHR$(143)+CHR$(8)+CHR$(11)+CHR$(244);:NEXT:NEXT
180 PLOT 0,20,4:DRAW 640,20,4
190 LOCATE 1,25:PEN 2:PRINT"SCORE";SCORE;:LOCATE 13,25:PRINT"HI";HISCORE;
200 :'
210 :'MAIN GAME
220 :'
230 LOCATE X-1,Y:PRINT "   ";
240 PEN 1:LOCATE X,Y:PRINT PLANE$;:PEN 2
250 IF Y=21 AND X=15 THEN GOTO 290:ELSE GOTO 340                
260 :'
270 :'LANDED
280 :'
290 FOR C=0 TO 1000:NEXT
300 SCORE=SCORE+100-(SKILL*2):SKILL=SKILL-1:X=2:Y=2:A=2:B=2:DROP=0
310 IF SKILL<10 THEN SKILL=10:RATE=RATE-20
320 IF RATE<0 THEN RATE=0
330 GOTO 150
340 FOR C=0 TO RATE:NEXT
350 X=X+1
360 IF X=18 THEN LOCATE X-1,Y:PRINT CHR$(18);:X=2:Y=Y+1:LOCATE X,Y:PEN 1:PRINT PLANE$;:PEN 2      
370 A$=INKEY$:IF A$=" " AND DROP=0 THEN DROP=1:B=Y+2:A=X 
380 IF Y=21 THEN DROP=0
390 IF DROP=1THEN LOCATE A,B:PRINT CHR$(252);:LOCATE A,B-1:PRINT"  ";:B=B+1:IF B>21 THEN LOCATE A,B:PRINT"  ";:LOCATE A,B-1:PRINT"  ";:A=0:B=0:DROP=0:SOUND 3,4000,10,12,0,0,10
400 GA=(A-0.5)*32:GB=400-(B*16):BOMB=TEST(GA,GB)
410 IF BOMB>0 THEN GOTO 670
420 GX=((X+1.5)*32):GY=408-(Y*16):CRASH=TEST(GX,GY)
430 IF CRASH>0 GOTO 570
440 GOTO 230
450 :'
460 :'INSTRUCTIONS
470 :'
480 LOCATE 1,2:PEN 1:PRINT"You are piloting an aircraft over a des-erted city and must clear the buildings in order to land and  refuel. Your air- craft moves across  the screen from leftto right.";:PRINT        
490 PRINT:PRINT"On reaching  the  right ,the aircraft   returns to the left A LINE FURTHER DOWN.You have unlimited supply of bombs, and you can drop them on the buildings belowby pressing the SPACE BAR.";:PRINT        
500 PRINT:PRINT"Each time you land, the height of the   buildings and the speed of your aircraftincreases.";:PRINT:PRINT:PRINT"ONCE YOU HAVE RELEASED A BOMB, YOU WILL NOT BE ABLE TO RELEASE ANOTHER UNTIL THEFIRST HAS EXPLODED !!!";   
510 PEN 2:LOCATE 1,24:PRINT:PRINT"PRESS ANY KEY TO START.";
520 A$=INKEY$:IF A$="" GOTO 520 
530 RETURN
540 :'
550 :'COLLISION
560 :'
570 LOCATE X-1,Y:PRINT CHR$(32)+CHR$(32)+CHR$(32)+CHR$(253)+CHR$(8)+CHR$(238)+CHR$(8);
580 FOR T=1 TO 10:SOUND 7,4000,5,15,0,0,5:PEN T:PRINT CHR$(253)+CHR$(8)+CHR$(238)+CHR$(8)+CHR$(32)+CHR$(8);:FOR TM=0 TO 50:NEXT:NEXT:PEN 2
590 CLS:LOCATE 1,5:PRINT"You scored";SCORE;
600 IF SCORE>HISCORE THEN HISCORE=SCORE:LOCATE 1,8:PRINT"TOP SCORE !!!!"; 
610 SCORE=0:LOCATE 1,12:PRINT"PRESS R TO RESTART";
620 A$=INKEY$:IF A$="r" OR A$="R" GOTO 630 ELSE GOTO 620    
630 PEN 1:MODE 1:X=2:Y=2:A=2:B=2:GOTO 90
640 :'
650 :'BOMBED BUILDING  
660 :'
670 LOCATE A,B-1:PRINT"  "+CHR$(8);:PEN 4:FOR TR=1 TO INT(RND(1)*3)+1:SCORE=SCORE+5:SOUND 3,4000,10,12,0,0,10:LOCATE A,B:FOR T=0 TO 4:PRINT CHR$(253)+CHR$(8)+CHR$(32)+CHR$(8);:NEXT:B=B+1
680 IF B=24 THEN B=B-1
690 NEXT
700 LOCATE 6,25:PRINT SCORE;:DROP=0:A=X:B=Y:GOTO 230
